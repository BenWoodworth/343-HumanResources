group 'swen343.hr'
version '1.0'

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

buildscript {
    ext {
        java_target = '1.8'

        version_kotlin = '1.1.51'
        version_ktlint = '0.8.1'
        version_spark = '1.0.0-alpha'
        version_freemarker = '2.3.14'
        version_slf4j = '1.6.1'
        version_guice = '4.0'
        version_mysql_connector = '5.1.13'
        version_moshi = '1.5.0'
        version_spek = '1.1.5'
        version_mockito = '1.10.19'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"

        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-runtime:$version_kotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$version_kotlin"

    ktlint 'com.github.shyiko:ktlint:0.8.1'

    compile "com.sparkjava:spark-kotlin:$version_spark"
    compile "org.freemarker:freemarker:$version_freemarker"

    compile "org.slf4j:slf4j-simple:$version_slf4j"
    compile "org.slf4j:slf4j-api:$version_slf4j"

    compile "com.google.inject:guice:$version_guice"

    compile "mysql:mysql-connector-java:$version_mysql_connector"

    compile "com.squareup.moshi:moshi:$version_moshi"

    testCompile "org.jetbrains.spek:spek-api:$version_spek"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$version_spek"
    testCompile "org.mockito:mockito-all:$version_mockito"
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['src/main/**/*.kt']
}

task ktlintFormat(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['-F', 'src/main/**/*.kt']
}

check {
    dependsOn ktlint
}

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    main.java.srcDirs += "$buildDir/generated/source/kapt/main"

    test.java.srcDirs += "src/test/kotlin"
}

jar {
    manifest {
        attributes 'Main-Class': 'swen343.hr.MainKt'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}
